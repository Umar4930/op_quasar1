plugins {
	id 'org.springframework.boot' version '2.3.3.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
}

group = 'com.rebell.alliance.intelligence'
version = '0.0.1'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.amazonaws.serverless:aws-serverless-java-container-springboot2:[1.0,)'
	implementation 'com.lemmingapex.trilateration:trilateration:1.0.2'
	implementation 'org.apache.commons:commons-math3:3.6.1'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation('org.assertj:assertj-core:3.17.2')
}

test {
	useJUnitPlatform()
}

task packageFat(type: Zip) {
	from compileJava
	from processResources
	into('lib') {
		from(configurations.runtimeClasspath) {
			exclude 'tomcat-embed-*'
		}
	}
}

task packageLibs(type: Zip) {
	into('java/lib') {
		from(configurations.runtimeClasspath) {
			exclude 'tomcat-embed-*'
		}
	}
}

task packageSkinny(type: Zip) {
	from compileJava
	from processResources
}

build.dependsOn packageFat